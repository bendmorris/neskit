import neskit.mappers.*;

struct Mapper {
    public var mapperType: MapperType;

    public var prgMap: CArray[Uint16, 32] = empty;

    public static function get(nes: Ptr[Nes], n: Int): Mapper using implicit nes {
        var mapperType: MapperType;
        match n {
            0 => mapperType = Nrom;
            // 1 => return Mmc1;
            2 => mapperType = Unrom(UnromMapper.new());
            // 3 => return Cnrom;
            // 4 => return Mmc3;
            7 => mapperType = Aorom(AoromMapper.new());
            default => {
                printf("unrecognized mapper: %i\n", n);
                exit(1);
            }
        }
        var mapper = struct Mapper {mapperType};
        return mapper;
    }

    public function init(nes: Ptr[Nes]) {
        for i in 0 ... this.prgMap.length {
            this.prgMap[i] = (0x400 * i) & (nes.rom.prgSize - 1);
        }
    }

    public function read(nes: Ptr[Nes], addr: Uint16): Uint8 using implicit nes {
        if addr >= 0x8000 {
            return nes.rom.prgRom[this.prgMap[(((addr & 0x7fff)) >> 10) & 0x1f] + (addr & 0x3ff)];
        } else if addr >= 0x6000 && nes.rom.hasPrgRam {
            return nes.rom.prgRam[addr & 0x1fff];
        } else {
            return addr >> 8;
        }
    }

    public function write(nes: Ptr[Nes], addr: Uint16, data: Uint8) using implicit nes {
        if addr >= 0x6000 && addr < 0x8000 {
            var a = addr & 0x1fff;
            nes.rom.sramDirty = nes.rom.sramDirty || nes.rom.prgRam[a] != data;
            nes.rom.prgRam[a] = data;
        }
    }

    public function onScanline(n: Uint8) {
        this.mapperType.onScanline(n);
    }
}

enum MapperType {
    Nrom;
    Aorom(x: AoromMapper);
    Unrom(x: UnromMapper);

    public function init() {
        match this {
            Aorom(x) => x.init();
            Unrom(x) => x.init();
        }
    }

    public function read(nes: Ptr[Nes], addr: Uint32): Uint8 using implicit nes {
        match this {
            Aorom(x) => return x.read(addr);
            Unrom(x) => return x.read(addr);
            default => return 0;
        }
    }

    public function write(nes: Ptr[Nes], addr: Uint32, value: Uint8) using implicit nes {
        match this {
            Aorom(x) => x.write(addr, value);
            Unrom(x) => x.write(addr, value);
        }
    }

    public function onScanline(n: Uint8) {}
}
